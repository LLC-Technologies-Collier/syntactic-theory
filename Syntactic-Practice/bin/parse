#!/usr/bin/perl

use strict;
use warnings;
use Carp;

use Getopt::Long::Descriptive;

use File::Basename;
use File::Spec;
use DateTime;

use Syntactic::Practice::Lexer;
use Syntactic::Practice::Phrase;
use Syntactic::Practice::Parser;

# TODO: variable glossary

my ( $getOpt, $usage ) =
  describe_options( 'parse %o <some-arg>',
                    [ 'file|f=s',
                      "file containing text to parse",
                      { required => 1 }
                    ],
                    [ 'title|t=s',
                      "Title for latex rendering of parses",
                      { default => "A title" }
                    ],
                    [ 'author|a=s',
                      "Author for latex rendering of parses",
                      { default => "C.J." }
                    ],
                    [],
                    [ 'verbose|v', "print extra stuff" ],
                    [ 'help',      "print usage message and exit" ], );

print( $usage->text ), exit if $getOpt->help;

print STDERR ( "=====\n" );

my $content = do {
  local undef $/;
  open( my $fh, q{<}, $getOpt->file )
    or die "could not open file [\($getOpt->file)]";
  <$fh>;
};

my ( $srcName, $srcPath, $suffix ) = fileparse( $getOpt->file, '.txt' );

chomp $content;

my $lexer     = Syntactic::Practice::Lexer->new();
my @paragraph = $lexer->scan( $content );
my @sentence  = @{ $paragraph[0] };
my @word_list = @{ $sentence[0] };

print STDERR "$content\n=====\n";

warn( scalar @word_list,
      " words in sentence\n",
      join( " ", map { ref $_ ? $_->word : $_ } @word_list ) );

my $parser = Syntactic::Practice::Parser->new();

my @constituent = $parser->ingest_phrase(
                                          { sentence => $sentence[0],
                                            rule     => 'S'
                                          } );

print map { $_->as_text } @constituent;

my $now = DateTime->now();

my $LaTeXfilename = File::Spec->catfile( $srcPath, $srcName . '.tex' );

open( my $fh, q{>}, $LaTeXfilename )
  or die "Couldn't open file [$LaTeXfilename]";

print $fh tree_as_latex(
                { constituent => \@constituent,
                  title       => $getOpt->title,
                  author      => $getOpt->author,
                  date => $now->month_name . ' ' . $now->day . ', ' . $now->year
                } );

sub tree_as_latex {
  my ( $opt ) = @_;

  my @package = qw(forest);

  my @forest =
    map { '\begin{forest}' . $_->as_forest() . '\end{forest}' }
    @{ $opt->{constituent} };

  return
    join( "\n",
          '\documentclass{report}', ( map { "\\usepackage{$_}\n" } @package ),
          "\\title{$opt->{title}}", "\\author{$opt->{author}}",
          "\\date{$opt->{date}}",   '\begin{document}',
          '\maketitle', join( '\newline', @forest ),
          '\end{document}', );
}

